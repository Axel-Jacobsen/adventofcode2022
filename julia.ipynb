{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "99a466e9-f1db-4fd2-beff-7b809e0e4c8d",
   "metadata": {},
   "source": [
    "# Advent of Code, 2022!\n",
    "\n",
    "It is truly the most wonderful time of the year! Here is my work for this year's Advent."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "9ac50f13-abd6-465f-bbf9-2c55b83d46ca",
   "metadata": {},
   "outputs": [],
   "source": [
    "using DataStructures\n",
    "using JupyterFormatter\n",
    "enable_autoformat()\n",
    "\n",
    "# Helpers, of course\n",
    "function quantify(predicate::Function, data)\n",
    "    mapreduce(predicate, +, data)\n",
    "end\n",
    "\n",
    "function process_inputs(convert::Function, day::String)\n",
    "    open(\"inputs/d$day.txt\", \"r\") do io\n",
    "        map(s -> convert(s), eachline(io))\n",
    "    end\n",
    "end\n",
    "\n",
    "function get_first(itr, pred::Function)\n",
    "    # there must be a native julia fcn to do this\n",
    "    for v in itr\n",
    "        if pred(v)\n",
    "            return v\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "process_inputs(day::String) = process_inputs(s -> s, day)\n",
    "process_inputs(::Type{Int}, day::String) = process_inputs(s -> parse(Int64, s), day)\n",
    "\n",
    "function matrix_inputs(day::String)\n",
    "    process_inputs(day) do line\n",
    "        # vector of ints\n",
    "        map(s -> parse(Int, s), collect(line))\n",
    "        # convert vec of vec of int to matrix of int\n",
    "    end |> vv -> mapreduce(permutedims, vcat, vv)\n",
    "end\n",
    "\n",
    "clockmod(i::Int, m::Int) = i % m == 0 ? m : i % m\n",
    "\n",
    "alphabet = \"abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ\"\n",
    "\n",
    "cartdirs = ((-1, 0), (1, 0), (0, -1), (0, 1))\n",
    "diagdirs = ((-1, 0), (1, 0), (0, -1), (0, 1), (-1, 1), (1, 1), (-1, -1), (1, -1))\n",
    "\n",
    "Base.:+(p1::Tuple{Int,Int}, p2::Tuple{Int,Int}) = (p1[1] + p2[1], p1[2] + p2[2])\n",
    "Base.:≤(p1::Tuple{Int,Int}, p2::Tuple{Int,Int}) = p1[1] ≤ p2[1] && p1[2] ≤ p2[2]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "918cd621-40bb-4337-87a2-b1e4e6fe31c8",
   "metadata": {},
   "source": [
    "## Day 1!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "843f9cdc-8c87-4b0e-91a1-049b204a2658",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(68802, 205370)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function p1()\n",
    "    raw_data = read(\"inputs/d01.txt\", String) |> strip |> s -> split(s, \"\\n\\n\")\n",
    "\n",
    "    chunks = map(s -> split(s, \"\\n\"), raw_data)\n",
    "\n",
    "    calories = map(chunks) do chunk\n",
    "        mapreduce(s -> parse(Int64, s), +, chunk)\n",
    "    end\n",
    "\n",
    "    maximum(calories)\n",
    "end\n",
    "\n",
    "\n",
    "function p2()\n",
    "    raw_data = read(\"inputs/d01.txt\", String) |> strip |> s -> split(s, \"\\n\\n\")\n",
    "\n",
    "    chunks = map(s -> split(s, \"\\n\"), raw_data)\n",
    "\n",
    "    calories = map(chunks) do chunk\n",
    "        mapreduce(s -> parse(Int64, s), +, chunk)\n",
    "    end |> sort\n",
    "\n",
    "    calories[end-2:end] |> sum\n",
    "end\n",
    "\n",
    "\n",
    "p1(), p2()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "afd26b81",
   "metadata": {},
   "source": [
    "## Day 2!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "9fabe85e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(12772, 11618)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ops = Dict(\"A\" => 0, \"X\" => 0, \"B\" => 1, \"Y\" => 1, \"C\" => 2, \"Z\" => 2)\n",
    "\n",
    "function rps(a, b)\n",
    "    if (ops[a] + 0) % 3 == ops[b]\n",
    "        3  # draw\n",
    "    elseif (ops[a] + 1) % 3 == ops[b]\n",
    "        0  # lose\n",
    "    elseif (ops[a] + 2) % 3 == ops[b]\n",
    "        6  # win\n",
    "    end\n",
    "end\n",
    "\n",
    "rps_val(c) = ops[c] + 1\n",
    "\n",
    "function p1()\n",
    "    raw_data = process_inputs(s -> split(s, \" \"), \"02\")\n",
    "    rps_value(pair) = rps(pair[2], pair[1]) + rps_val(pair[2])\n",
    "    mapreduce(rps_value, +, raw_data)\n",
    "end\n",
    "\n",
    "function p2()\n",
    "    throw_idxs = [\"A\", \"B\", \"C\"]\n",
    "    result_matrix = [\n",
    "        [\"Y\" \"X\" \"Z\"]\n",
    "        [\"Z\" \"Y\" \"X\"]\n",
    "        [\"X\" \"Z\" \"Y\"]\n",
    "    ]\n",
    "    raw_data = process_inputs(s -> split(s, \" \"), \"02\")\n",
    "\n",
    "    function strategy(pair)\n",
    "        their_throw = findfirst(s -> s == pair[1], throw_idxs)\n",
    "        my_throw_idx = findfirst(s -> s == pair[2], result_matrix[:, their_throw])\n",
    "        my_throw = throw_idxs[my_throw_idx]\n",
    "        rps(my_throw, pair[1]) + rps_val(my_throw)\n",
    "    end\n",
    "    mapreduce(strategy, +, raw_data)\n",
    "end\n",
    "\n",
    "p1(), p2()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ae924dcc",
   "metadata": {},
   "source": [
    "## Day 3!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "3be074a7",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(8243, 2631)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function p1()\n",
    "    data = process_inputs(\n",
    "        # split each line in half and take the common letter in each half\n",
    "        s -> intersect(Set(s[1:length(s)÷2]), Set(s[length(s)÷2+1:end])),\n",
    "        \"03\",\n",
    "    )\n",
    "    mapreduce(+, data) do sv\n",
    "        # for each letter, get it's value\n",
    "        letter = pop!(sv)\n",
    "        findfirst(s -> s == letter, alphabet)\n",
    "    end\n",
    "end\n",
    "\n",
    "group_n(arr, n::Int) = [arr[i:i+n-1] for i = 1:n:length(arr)]\n",
    "\n",
    "function p2()\n",
    "    # get each line of data for day 3 as a vector of strings\n",
    "    data = process_inputs(\"03\")\n",
    "    elf_groups = group_n(data, 3)\n",
    "    elf_group_ids = [pop!(intersect(g...)) for g in elf_groups]\n",
    "\n",
    "    mapreduce(+, elf_group_ids) do letter\n",
    "        findfirst(s -> s == letter, alphabet)\n",
    "    end\n",
    "end\n",
    "\n",
    "p1(), p2()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e77b16ae",
   "metadata": {},
   "source": [
    "## Day 4!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "63c0460f",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(509, 870)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function get_d04_data()\n",
    "    process_inputs(\"04\") do str\n",
    "        # parse eg string \"5-7,4-9\" into Vector{UnitRange}[5:7,4:9]\n",
    "        elf_assignment_strs = split(str, \",\")\n",
    "        elf_assignment_ranges = map(elf_assignment_strs) do s\n",
    "            (s0, s1) = split(s, \"-\")\n",
    "            parse(Int64, s0):parse(Int64, s1)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function p1()\n",
    "    quantify(get_d04_data()) do (r1, r2)\n",
    "        # if the intersect is r1 or r2, r1 or\n",
    "        # r2 is entirely overlaps the other\n",
    "        intersect(r1, r2) in (r1, r2)\n",
    "    end\n",
    "end\n",
    "\n",
    "function p2()\n",
    "    quantify(get_d04_data()) do (r1, r2)\n",
    "        # if the intersect is empty, r1 and\n",
    "        # r2 do not overlap\n",
    "        intersect(r1, r2) |> collect |> isempty |> Base.:!\n",
    "    end\n",
    "end\n",
    "\n",
    "p1(), p2()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e002572a",
   "metadata": {},
   "source": [
    "## Day 5!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "eee3bff6",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(\"QGTHFZBHV\", \"MGDMPSZTM\")"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function process_init_str_state(init_str_state)\n",
    "    cleaned_strs = split(init_str_state, \"\\n\")\n",
    "    stack_strs = cleaned_strs[1:end-1, 1]\n",
    "    numbers_str = cleaned_strs[end, 1]\n",
    "\n",
    "    # get indexes of columns of boxes\n",
    "    indexes::Vector{Int} = []\n",
    "    for (i, col) in enumerate(numbers_str)\n",
    "        if isdigit(col)\n",
    "            push!(indexes, i)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # create actual stacks of chars\n",
    "    stacks = [Stack{Char}() for _ = 1:length(indexes)]\n",
    "    for (j, index) in enumerate(indexes)\n",
    "        for k = length(stack_strs):-1:1\n",
    "            letter = stack_strs[k][index]\n",
    "            if letter != ' '\n",
    "                push!(stacks[j], letter)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    stacks\n",
    "end\n",
    "\n",
    "function process_command_strs(command_strs)\n",
    "    map(split(strip(command_strs), \"\\n\")) do line\n",
    "        # format is 'move n from x to y'\n",
    "        num_strs = match(r\"move (\\d+) from (\\d+) to (\\d+)\", line)\n",
    "        (n, from, to) = map(s -> parse(Int64, s), num_strs.captures)\n",
    "        (n, from, to)\n",
    "    end\n",
    "end\n",
    "\n",
    "function process_d05_inputs()\n",
    "    (initial_str_state, command_strs) =\n",
    "        read(\"inputs/d05.txt\", String) |> s -> split(s, \"\\n\\n\")\n",
    "\n",
    "    stacks = initial_str_state |> process_init_str_state\n",
    "    commands = command_strs |> process_command_strs\n",
    "    stacks, commands\n",
    "end\n",
    "\n",
    "function peek_top_boxes(stacks)\n",
    "    map(first, stacks) |> String\n",
    "end\n",
    "\n",
    "\n",
    "function p1()\n",
    "    stacks, commands = process_d05_inputs()\n",
    "    for command in commands\n",
    "        n, from, to = command\n",
    "        for i = 1:n\n",
    "            push!(stacks[to], pop!(stacks[from]))\n",
    "        end\n",
    "    end\n",
    "    peek_top_boxes(stacks)\n",
    "end\n",
    "\n",
    "function p2()\n",
    "    stacks, commands = process_d05_inputs()\n",
    "    for command in commands\n",
    "        n, from, to = command\n",
    "        from_vals = [pop!(stacks[from]) for _ = 1:n]\n",
    "        for v in Iterators.reverse(from_vals)\n",
    "            push!(stacks[to], v)\n",
    "        end\n",
    "    end\n",
    "    peek_top_boxes(stacks)\n",
    "end\n",
    "\n",
    "p1(), p2()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2f087d6b",
   "metadata": {},
   "source": [
    "## Day 6!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "7640fc7e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(1855, 3256)"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function first_n_unique(str::String, n::Int)::Int\n",
    "    chrs = Queue{Char}()\n",
    "    for (i, letter) in enumerate(str)\n",
    "        if length(chrs) == n\n",
    "            dequeue!(chrs)\n",
    "        end\n",
    "        enqueue!(chrs, letter)\n",
    "        if unique(chrs) |> length == n\n",
    "            return i\n",
    "        end\n",
    "    end\n",
    "    i\n",
    "end\n",
    "\n",
    "start_of_packet_idx(data) = first_n_unique(data, 4)\n",
    "start_of_message_idx(data) = first_n_unique(data, 14)\n",
    "\n",
    "\n",
    "function p1()\n",
    "    data = read(\"inputs/d06.txt\", String)\n",
    "    start_of_packet_idx(data)\n",
    "end\n",
    "\n",
    "function p2()\n",
    "    data = read(\"inputs/d06.txt\", String)\n",
    "    start_of_message_idx(data)\n",
    "end\n",
    "\n",
    "p1(), p2()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ceeb3597",
   "metadata": {},
   "source": [
    "## Day 7!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "7f78dc7c",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(1644735, 1300850)"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "struct File\n",
    "    name::AbstractString\n",
    "    size::Int\n",
    "end\n",
    "\n",
    "mutable struct Folder\n",
    "    name::AbstractString\n",
    "    parent::Union{Folder,Nothing}\n",
    "    files::Vector{Union{File,Folder}}\n",
    "\n",
    "    Folder(name::AbstractString) = new(name, nothing, Vector{File}())\n",
    "    Folder(name::AbstractString, parent::Folder) = new(name, parent, Vector{File}())\n",
    "    Folder(name::String, parent::Folder, files::Vector{File}) = new(name, files)\n",
    "end\n",
    "\n",
    "\n",
    "is_cmd = startswith(\"\\$ \")\n",
    "is_dir = startswith(\"dir \")\n",
    "is_file = startswith(r\"\\d+\")\n",
    "\n",
    "function get_folder(folder_str::String, current_dir::Folder)::Folder\n",
    "    name = split(folder_str, \" \")[end]\n",
    "    Folder(name, current_dir)\n",
    "end\n",
    "\n",
    "function get_file(file_str)::File\n",
    "    (filesize_str, name) = split(file_str, \" \")\n",
    "    File(name, parse(Int, filesize_str))\n",
    "end\n",
    "\n",
    "function get_cmd(s)::Vector{String}\n",
    "    cmd_vec = replace(s, \"\\$ \" => \"\") |> s -> split(s, \" \")\n",
    "    if length(cmd_vec) == 1\n",
    "        # if cmd doesn't have args, just set arg to be \"\"\n",
    "        Vector{String}([cmd_vec[1], \"\"])\n",
    "    else\n",
    "        cmd_vec\n",
    "    end\n",
    "end\n",
    "\n",
    "function execute_cd(folder::Folder, name::String)::Folder\n",
    "    if name == \"..\"\n",
    "        folder.parent\n",
    "    else\n",
    "        get_first(folder.files, f -> f.name == name)\n",
    "    end\n",
    "end\n",
    "\n",
    "function process_terminal_command_history(day)\n",
    "    data = process_inputs(day)\n",
    "    num_lines = length(data)\n",
    "\n",
    "    @assert (data[1] == \"\\$ cd /\")\n",
    "\n",
    "    root_folder = Folder(\"/\")\n",
    "    current_folder = root_folder\n",
    "\n",
    "    i = 2\n",
    "    next_line = data[i]\n",
    "    while i <= num_lines\n",
    "        @assert is_cmd(next_line)\n",
    "        cmd, arg = get_cmd(next_line)\n",
    "\n",
    "        if cmd == \"ls\"\n",
    "            i += 1\n",
    "            next_line = data[i]\n",
    "            while !is_cmd(next_line)\n",
    "                if is_dir(next_line)\n",
    "                    push!(current_folder.files, get_folder(next_line, current_folder))\n",
    "                elseif is_file(next_line)\n",
    "                    push!(current_folder.files, get_file(next_line))\n",
    "                else\n",
    "                    @warn \"unknown line in ls $(next_line)\"\n",
    "                end\n",
    "                i += 1\n",
    "                if i > num_lines\n",
    "                    break\n",
    "                end\n",
    "                next_line = data[i]\n",
    "            end\n",
    "        elseif cmd == \"cd\"\n",
    "            current_folder = execute_cd(current_folder, arg)\n",
    "            i += 1\n",
    "            next_line = data[i]\n",
    "        else\n",
    "            @warn \"unknown cmd $(cmd)\"\n",
    "        end\n",
    "    end\n",
    "    root_folder\n",
    "end\n",
    "\n",
    "function get_folder_size(\n",
    "    folder::Folder,\n",
    "    subfolders::Union{Vector{Tuple{Folder,Int}},Nothing} = nothing,\n",
    ")\n",
    "    total_fs = mapreduce(+, folder.files) do fd\n",
    "        if isa(fd, File)\n",
    "            fd.size\n",
    "        else\n",
    "            folder_size, _ = get_folder_size(fd, subfolders)\n",
    "            if subfolders != nothing\n",
    "                push!(subfolders, (fd, folder_size))\n",
    "            end\n",
    "            folder_size\n",
    "        end\n",
    "    end\n",
    "\n",
    "    if subfolders != nothing\n",
    "        total_fs, subfolders\n",
    "    else\n",
    "        total_fs\n",
    "    end\n",
    "end\n",
    "\n",
    "function p1()\n",
    "    root_folder = process_terminal_command_history(\"07\")\n",
    "    root_folder_size, folder_sizes =\n",
    "        get_folder_size(root_folder, Vector{Tuple{Folder,Int}}())\n",
    "\n",
    "    s = 0\n",
    "    for (folder, fs) in folder_sizes\n",
    "        if fs <= 100000\n",
    "            s += fs\n",
    "        end\n",
    "    end\n",
    "    s\n",
    "end\n",
    "\n",
    "function p2()\n",
    "    root_folder = process_terminal_command_history(\"07\")\n",
    "    root_folder_size, folder_sizes =\n",
    "        get_folder_size(root_folder, Vector{Tuple{Folder,Int}}())\n",
    "\n",
    "    mem_required_for_update = root_folder_size - (70000000 - 30000000)\n",
    "    for (folder, fs) in sort(folder_sizes, by = x -> x[2])\n",
    "        if fs > mem_required_for_update\n",
    "            return fs\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "p1(), p2()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
